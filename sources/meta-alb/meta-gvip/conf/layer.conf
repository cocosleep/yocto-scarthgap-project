# We have a conf and classes directory, add to BBPATH.
BBPATH .= ":${LAYERDIR}"

LICENSE_PATH:append = " ${LAYERDIR}/custom-licenses"
GOLDVIP_SOFTWARE_LICENSE = "${LAYERDIR}/custom-licenses/LA_OPT_NXP_Software_License" 

BBFILE_COLLECTIONS:append = " goldvip"
BBFILE_PATTERN_goldvip = "^${LAYERDIR}/"
BBFILE_PRIORITY_goldvip = "10"

# The dynamic-layers directory hosts the extensions and layer specific
# modifications related to NXP products.
#
# The .bbappend and .bb files are included if the respective layer
# collection is available.
BBFILES += "${@' '.join('${LAYERDIR}/dynamic-layers/%s/recipes*/*/*.bbappend' % layer \
                for layer in BBFILE_COLLECTIONS.split())}"
BBFILES += "${@' '.join('${LAYERDIR}/dynamic-layers/%s/recipes*/*/*.bb' % layer \
                for layer in BBFILE_COLLECTIONS.split())}"

# We have recipes-* directories, add to BBFILES.
BBFILES += " \
    ${LAYERDIR}/recipes-*/*/*.bb \
    ${LAYERDIR}/recipes-*/*/*.bbappend \
"

LAYERSERIES_COMPAT_goldvip = "kirkstone"

ALB_RELEASE = "${@d.getVar('RELEASE_BASE').replace('release/','').replace('bsp','').replace('_','')}"
GOLDVIP_RELEASE = "1.12.0"

