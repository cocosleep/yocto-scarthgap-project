# We have a conf and classes directory, add to BBPATH
# Note how we PREpend BBPATH. This is due to the fact that
# bitbake does not override classes per layer as it does
# recipes. Classes are processed strictly in BBPATH order.
# As we unfortunately have to override standard classes for
# bug fixing, we prepend instead of append our path.
BBPATH =. "${LAYERDIR}:"

FSL_EULA_FILE = "${LAYERDIR}/EULA"

BBFILE_COLLECTIONS += "alb-sdk"
BBFILE_PATTERN_alb-sdk := "^${LAYERDIR}"
BBFILE_PRIORITY_alb-sdk = "9"

# The dynamic-layers directory hosts the extensions and layer specific
# modifications related to NXP products.
#
# The .bbappend and .bb files are included if the respective layer
# collection is available.

BBFILES_DYNAMIC += "\
        ${@' '.join('%s:${LAYERDIR}/dynamic-layers/%s/recipes*/*/*.bbappend' % (layer, layer) \
        for layer in BBFILE_COLLECTIONS.split())} \
"

BBFILES_DYNAMIC += "\
        ${@' '.join('%s:${LAYERDIR}/dynamic-layers/%s/recipes*/*/*.bb' % (layer, layer) \
        for layer in BBFILE_COLLECTIONS.split())} \
"

BBFILES_DYNAMIC += "\
        linaro-toolchain:${LAYERDIR}/dynamic-layers/linaro-toolchain/*/*.bbappend \
"

# We want our meta-alb recipes to be applied *after* the
# dynamic layer changes. This is for us to use the dynamic-layers to
# override existing layers before we apply our changes
BBFILES += " \
        ${LAYERDIR}/recipes-*/*/*.bb \
        ${LAYERDIR}/recipes-*/*/*.bbappend \
        ${LAYERDIR}/classes/*.bb* \
"

# Set LAYERSERIES_COMPAT variable for meta-alb-sdk layer
# to match the version "kirkstone" of OpenEmbedded-Core
LAYERSERIES_COMPAT_alb-sdk = "kirkstone"

RELEASE_BASE ?= "release/bsp42.0"
